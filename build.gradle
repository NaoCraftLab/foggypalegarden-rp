plugins {
    id 'base'
    id 'com.naocraftlab.apply-ext-properties-plugin'
}

def resourcePackDir = layout.buildDirectory.dir('files')

tasks.register('assembleResourcePack', Copy) {
    from('src/main/resources') {
        filter { line ->
            line.replaceAll(/\$\{(\w+)\}/) { fullMatch, propName ->
                //noinspection GroovyAssignabilityCheck
                project.findProperty(propName)?.toString() ?: "\${$propName}"
            }
        }
    }
    from 'LICENSE'
    from 'CHANGELOG.md'
    from ('.idea/icon.png') {
        rename { 'pack.png' }
    }

    into resourcePackDir
}

tasks.register('packageResourcePack', Zip) {
    dependsOn 'assembleResourcePack'

    from resourcePackDir
    inputs.dir resourcePackDir

    archiveFileName.set("$rpName $rpVersion+${specMcVersions.split(',')[0].trim()}" + '.zip')

    destinationDirectory.set(layout.buildDirectory)
}

tasks.named('assemble') {
    dependsOn 'assembleResourcePack'
    dependsOn 'packageResourcePack'
}
